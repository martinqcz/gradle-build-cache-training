plugins {
    id 'com.gradle.build-scan' version '1.12.1'
    id 'base'
}

buildScan {
    server = 'https://enterprise-training.gradle.com'
    publishAlways()
}

ext {
    baseVersion = '1.0'
    timestamp = java.time.Instant.now().toEpochMilli()
}

subprojects {
    apply plugin: 'java-library'
    sourceCompatibility = 8
    targetCompatibility = 8

    version = "${baseVersion}-${timestamp}"

    tasks.withType(Jar) { jar ->
        jar.version = baseVersion
    }

    // this just guarantees that everyone running the lab pushes their own changes into the remote cache,
    // rather than getting hits from someone else's run
    tasks.create('generateLocalUniqueValue') {
        ext.outputFile = file("$projectDir/local.txt")
        outputs.file(outputFile)
        doLast {
            byte[] buf = new byte[20]
            new Random().nextBytes(buf)
            outputFile.bytes = buf
        }
    }

    configure([compileJava, compileTestJava, test]) {
        inputs.files(tasks.generateLocalUniqueValue).withPropertyName('uniqueValue')
    }
}
